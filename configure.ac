dnl Process this file with autoconf to produce a configure script.

AC_INIT([xournal],[0.4.8.2016])
AC_CONFIG_SRCDIR([src/xournal.h])
AM_INIT_AUTOMAKE([foreign])
AC_CONFIG_HEADERS([config.h])

IT_PROG_INTLTOOL

AC_PROG_CC
AC_PROG_RANLIB
AM_PROG_AR
AC_HEADER_STDC

AC_SEARCH_LIBS([strerror],[cposix])
AC_SEARCH_LIBS([atan2],[m])
AC_SEARCH_LIBS([hypot],[m])
AC_SEARCH_LIBS([gzprintf],[z])
AC_SEARCH_LIBS([XUngrabButton],[X11])

ifdef([PKG_CHECK_MODULES], [[]], [m4_fatal([Module checker (pkg-config or pkgconf) is required but does not seem to be installed.])])

pkg_modules="gtk+-2.0 >= 2.10.0 libgnomecanvas-2.0 >= 2.4.0 poppler-glib >= 0.5.4 pangoft2 >= 1.0"
PKG_CHECK_MODULES(PACKAGE,[$pkg_modules])
CFLAGS="$PACKAGE_CFLAGS $CFLAGS"
LIBS="$PACKAGE_LIBS $LIBS"

dnl detect Windows environment...
AC_CANONICAL_HOST
AC_MSG_CHECKING([for Windows environment])
case "$host" in
 *-*-mingw*)
   os_win32=yes;;
 *)
   os_win32=no;;
esac
AC_MSG_RESULT([$os_win32])
AM_CONDITIONAL(WIN32, test "$os_win32" = "yes")

GETTEXT_PACKAGE=xournal
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package.])

dnl Add the languages which your application supports here.
ALL_LINGUAS="`grep -v '^#' "$srcdir/po/LINGUAS" | tr '\n' ' '`"
AM_GLIB_GNU_GETTEXT

AC_CONFIG_FILES([
Makefile
src/Makefile
src/ttsubset/Makefile
po/Makefile.in
])
AC_OUTPUT

